<?xml version="1.0"?>

<root>

<merge mode="blend" />

<!-- scene pass -->
<pass
	enable="true"
	drawLine="false"
>
	<standard
		ambient="0.2, 0.2, 0.2, 1.0"
		diffuse="1.0, 1.0, 1.0, 1.0"
		specular="0.5, 0.5, 0.5, 1.0"
		shininess="30.0"
	/>

	<!-- The first occuring texture will use texture unit 0 -->

	<shader>
		<vertex>
<![CDATA[
#version 120
varying vec3 var_Vertex, var_Normal, var_LightPos;

uniform vec3 g_sunPos;

void main()
{
	gl_Position = ftransform();
	
	// viewspace position
	var_Vertex = (gl_ModelViewMatrix * gl_Vertex).xyz;
	
	// viewspace normal, no need to do normalize since we do it on fragment shader anyway
	var_Normal = gl_NormalMatrix * gl_Normal;
	
	// viewspace lightDir
	var_LightPos = (gl_ModelViewMatrix * vec4(g_sunPos, 1.0)).xyz;
	
	// texture coordinate
	gl_TexCoord[0] = gl_MultiTexCoord0;
}
]]>
		<vertex/>
		
		<fragment>
<![CDATA[
#version 120 
varying vec3 var_Vertex, var_Normal, var_LightPos;

void main()
{
	vec4 ambient = gl_FrontLightProduct[0].ambient;
	vec4 diffuse = gl_FrontLightProduct[0].diffuse;
	vec4 specular = gl_FrontLightProduct[0].specular;
	
	float ndotl = dot( normalize(var_LightPos - var_Vertex), normalize(var_Normal) );
	ndotl = ndotl * 0.5 + 0.5;
	
	gl_FragColor.rgb = ndotl * diffuse.rgb;
	gl_FragColor.a = diffuse.a;
}
]]>
		</fragment>
	</shader>
</pass>

<!-- skybox pass -->
<pass
	enable="true"
	drawLine="false"
>
	<shader>
		<vertex>
<![CDATA[
#version 120
varying vec3 var_Vertex;

void main()
{
	gl_Position = ftransform();
	
	// viewspace position
	var_Vertex = gl_Vertex.xyz;
}
]]>
		<vertex/>
		
		<fragment>
<![CDATA[
#version 120 
varying vec3 var_Vertex;
uniform vec3 g_sunPos;

void main()
{	
	vec3 g_skyDir = vec3(0.0, 1.0, 0.0);
	vec3 g_skyColorLow = vec3(0.75, 0.75, 1.0);
	vec3 g_skyColorHi = vec3(0.25, 0.25, 1.0);
	vec3 g_SunColor = vec3(1.0, 1.0, 1.0);
	float g_SunPower = 64.0;
	
	vec3 fragDir = normalize(var_Vertex);
	
	float dotSky = dot(fragDir, g_skyDir);
	vec3 skyColor = mix(g_skyColorLow, g_skyColorHi, dotSky * 0.5 + 0.5);
	
	float dotSun = dot(fragDir, normalize(g_sunPos));
	vec3 sunColor = pow(max(0, dotSun), g_SunPower) * g_SunColor;
	
	gl_FragColor = vec4(skyColor + sunColor, 1.0);
}
]]>
		</fragment>
	</shader>
</pass>

<!-- down-sample pass -->
<pass
	enable="true"
	drawLine="false"
>
	<!-- The first occuring texture will use texture unit 0 -->

	<shader>
		<vertex>
<![CDATA[
#version 120
uniform vec2 g_InvTexSize;

void main()
{
	gl_Position = ftransform();
	vec2 baseUV = gl_MultiTexCoord0.xy;
	gl_TexCoord[0].xy = baseUV + vec2(0, 0) * g_InvTexSize;
	gl_TexCoord[0].zw = baseUV + vec2(0, 0) * g_InvTexSize;
	gl_TexCoord[1].xy = baseUV + vec2(0, 0) * g_InvTexSize;
	gl_TexCoord[1].zw = baseUV + vec2(0, 0) * g_InvTexSize;
}
]]>
		<vertex/>
		
		<fragment>
<![CDATA[
#version 120
uniform sampler2D g_inputSam;

void main()
{
	vec4 sum = vec4(0.0, 0.0, 0.0, 0.0);
	sum += texture2D(g_inputSam, gl_TexCoord[0].xy);
	sum += texture2D(g_inputSam, gl_TexCoord[0].zw);
	sum += texture2D(g_inputSam, gl_TexCoord[1].xy);
	sum += texture2D(g_inputSam, gl_TexCoord[1].zw);
	
	gl_FragColor = sum * 0.25;
}
]]>
		</fragment>
	</shader>
</pass>

<!-- blur pass -->
<pass
	enable="true"
	drawLine="false"
>
	<!-- The first occuring texture will use texture unit 0 -->

	<shader>
		<vertex>
<![CDATA[
#version 120

#define NSAMPLES_2	5
#define NSAMPLES	(NSAMPLES_2 * 2)

uniform vec2 g_blurOffset[NSAMPLES];
uniform vec2 g_InvTexSize;

void main()
{
	gl_Position = ftransform();
	vec2 baseUV = gl_MultiTexCoord0.xy;
	
	int i = 0;
	for(int tu = 0; tu < NSAMPLES_2; ++tu)
	{
		gl_TexCoord[tu].xy = baseUV + g_blurOffset[i] * g_InvTexSize; ++i;
		gl_TexCoord[tu].zw = baseUV + g_blurOffset[i] * g_InvTexSize; ++i;
	}
}
]]>
		<vertex/>
		
		<fragment>
<![CDATA[
#version 120

#define NSAMPLES_2	5
#define NSAMPLES	(NSAMPLES_2 * 2)

uniform sampler2D g_inputSam;
uniform float g_blurKernel[NSAMPLES];

void main()
{
	vec4 sum = vec4(0.0, 0.0, 0.0, 0.0);
	
	int i = 0;
	for(int tu = 0; tu < NSAMPLES_2; ++tu)
	{
		sum += texture2D(g_inputSam, gl_TexCoord[tu].xy) * g_blurKernel[i]; ++i;
		sum += texture2D(g_inputSam, gl_TexCoord[tu].zw) * g_blurKernel[i]; ++i;
	}
	
	gl_FragColor = sum;
}
]]>
		</fragment>
	</shader>
</pass>

<!-- copy pass -->
<pass
	enable="true"
	drawLine="false"
>
	<!-- The first occuring texture will use texture unit 0 -->

	<shader>
		<vertex>
<![CDATA[
#version 120
void main()
{
	gl_Position = ftransform();
	gl_TexCoord[0] = gl_MultiTexCoord0;
}
]]>
		<vertex/>
		
		<fragment>
<![CDATA[
#version 120
uniform sampler2D g_inputSam;

void main()
{
	gl_FragColor = texture2D(g_inputSam, gl_TexCoord[0].xy);
}
]]>
		</fragment>
	</shader>
</pass>

<!-- radial-mask pass -->
<pass
	enable="true"
	drawLine="false"
>
	<!--blending colorBlend="true" srcFactor="one" dstFactor="one" /-->
	<!-- The first occuring texture will use texture unit 0 -->
	<texture
		file="../White2x2.png"
		shaderName="g_inputSam"
	/>
	<texture
		file="../SunRadialGlow_Mask.dds"
		shaderName="g_maskSam"
	/>
	
	<shader>
		<vertex>
<![CDATA[
#version 120

uniform vec3 g_sunPos;

void main()
{
	vec4 clipPos = vec4(gl_Vertex.xy, 0, 1);
	vec2 uv = gl_Vertex.xy * 0.5 + 0.5;
	
	vec4 sunPosCs = gl_ModelViewProjectionMatrix * vec4(g_sunPos, 1);
	vec2 ts = sunPosCs.xy / sunPosCs.ww;
	ts = ts * 0.5 + 0.5;
	
	gl_Position = clipPos;
	
	gl_TexCoord[0].xy = uv;
	gl_TexCoord[0].zw = (uv - ts.xy) * 0.5 + 0.5;
}
]]>
		<vertex/>
		
		<fragment>
<![CDATA[
#version 120

uniform sampler2D g_inputSam;
uniform sampler2D g_maskSam;

void main()
{
	vec4 glowClr = texture2D(g_inputSam, gl_TexCoord[0].xy);
	
	vec2 maskUV = gl_TexCoord[0].zw;
	maskUV = clamp(maskUV, vec2(0,0), vec2(1,1));
	
	vec4 maskClr = texture2D(g_maskSam, maskUV);
	
	gl_FragColor = glowClr * maskClr;
}
]]>
		</fragment>
	</shader>
</pass>

<!-- radial-glow pass -->
<pass
	enable="true"
	drawLine="false"
>
	<blending colorBlend="true" srcFactor="one" dstFactor="one" />
	
	<shader>
		<vertex>
<![CDATA[
#version 120

uniform vec3 g_sunPos;

void main()
{
	vec4 clipPos = vec4(gl_Vertex.xy, 0, 1);
	vec2 uv = gl_Vertex.xy * 0.5 + 0.5;
	
	vec4 sunPosCs = gl_ModelViewProjectionMatrix * vec4(g_sunPos, 1);
	vec2 ts = sunPosCs.xy / sunPosCs.ww;
	ts = ts * 0.5 + 0.5;
	
	gl_Position = clipPos;
	
	gl_TexCoord[0].xy = uv;
	gl_TexCoord[0].zw = (uv - ts.xy) * 0.5 + 0.5;
}
]]>
		<vertex/>
		
		<fragment>
<![CDATA[
#version 120

uniform sampler2D g_inputSam;

void main()
{
	vec4 glowClr = texture2D(g_inputSam, gl_TexCoord[0].xy);
	gl_FragColor = glowClr;
}
]]>
		</fragment>
	</shader>
</pass>
</root>